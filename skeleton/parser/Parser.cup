package parser;

import java.util.*;
import java.io.*;
import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory;

import ast.*;
import interpreter.Interpreter;

parser code {: 
  Lexer lexer;

  public Parser(Lexer lex, ComplexSymbolFactory sf) {
    super(lex, sf);
    lexer = lex;
  }

  public void report_fatal_error(String message, Object info) {
    Interpreter.fatalError(message + " (" + info + ")", Interpreter.EXIT_PARSING_ERROR);
  }

  ast.Location loc(int line, int column) {
    return new ast.Location(line, column);
  }

:};

terminal PLUS, MINUS, LPAREN, RPAREN, RETURN, SEMICOLON, TIMES, UMINUS,
         INT, LCURLY, RCURLY, ASSIGN, IF, ELSE, PRINT, COMMA,
         GE, LE, GT, LT, EQ, NE, AND, OR, NOT;
terminal Long    INTCONST;
terminal String   IDENT;


non terminal Program         program;
non terminal FuncDefList     funcDefList;
non terminal FuncDef         funcDef;
non terminal FormalDeclList  formalDeclList;
non terminal FormalDeclList  neFormalDeclList;
non terminal ExprList        exprList;
non terminal ExprList        neExprList;
non terminal Stmt            stmt;
non terminal StmtList        stmtList;
non terminal Expr            expr;
non terminal BinaryExpr      binaryExpr;
non terminal Cond            cond;

precedence left OR;
precedence left AND;
precedence left NOT;
precedence left GE, LE, GT, LT, EQ, NE;
precedence left PLUS, MINUS;
precedence left TIMES;
precedence left UMINUS;

precedence left ELSE;


start with program;

program ::= funcDefList:fdl
            {: RESULT = new Program(fdl, loc(fdlleft, fdlright)); :}
         ;
      
funcDefList ::= funcDef:first funcDefList:rest
            {: RESULT = new FuncDefList(first, rest, loc(firstleft, firstright)); :}
         | /* empty string */
            {: RESULT = null; :}
         ;

funcDef ::= INT IDENT:funcName LPAREN formalDeclList:params RPAREN LCURLY stmtList:body RCURLY
            {: RESULT = new FuncDef(funcName, params, body, loc(funcNameleft, funcNameright)); :}
         ;

formalDeclList ::= neFormalDeclList:list 
            {: RESULT = list; :}
         | /* empty string */
            {: RESULT = null; :}
         ;

neFormalDeclList ::= INT IDENT:first COMMA neFormalDeclList:rest
            {: RESULT = new FormalDeclList(first, rest, loc(firstleft, firstright)); :}
         | INT IDENT:first
            {: RESULT = new FormalDeclList(first, null, loc(firstleft, firstright)); :}
         ;

exprList ::= neExprList:list 
            {: RESULT = list; :}
         | /* empty string */
            {: RESULT = null; :}
         ;

neExprList ::= expr:first COMMA neExprList:rest
            {: RESULT = new ExprList(first, rest, loc(firstleft, firstright)); :}
         | expr:first
            {: RESULT = new ExprList(first, null, loc(firstleft, firstright)); :}
         ;

 
stmtList ::= stmt:s stmtList:sl
            {: RESULT = new StmtList(s, sl, loc(sleft, sright)); :}
         | /* empty string */
            {: RESULT = null; :}
         ;

stmt ::= RETURN expr:e SEMICOLON
            {: RESULT = new ReturnStmt(e, loc(eleft, eright)); :}
         | INT IDENT:varName ASSIGN expr:e SEMICOLON
            {: RESULT = new DeclStmt(varName, e, loc(varNameleft, varNameright)); :}
         | IF LPAREN cond:c RPAREN stmt:s
            {: RESULT = new IfStmt(c, s, null, loc(cleft, cright)); :}
         | IF LPAREN cond:c RPAREN stmt:s1 ELSE stmt:s2
            {: RESULT = new IfStmt(c, s1, s2, loc(cleft, cright)); :}
         | PRINT expr:e SEMICOLON
            {: RESULT = new PrintStmt(e, loc(eleft, eright)); :}
         | LCURLY stmtList:sl RCURLY
            {: RESULT = sl; :}
         ;

expr ::= INTCONST:c
         {: RESULT = new ConstExpr(c, loc(cleft, cright)); :}
      | IDENT:varName
         {: RESULT = new IdentExpr(varName, loc(varNameleft, varNameright)); :}
      |  binaryExpr:e
         {: RESULT = e; :}
      |  LPAREN expr:e RPAREN
         {: RESULT = e; :}
      | MINUS expr:e
         {: RESULT = new UnaryMinusExpr(e, loc(eleft, eright)); :}
         %prec UMINUS
      | IDENT:funcName LPAREN exprList:args RPAREN
         {: RESULT = new CallExpr(funcName, args, loc(funcNameleft, funcNameright)); :} 
      ;

binaryExpr ::= expr:e1 PLUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.PLUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 MINUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.MINUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 TIMES expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.TIMES, e2, loc(e1left, e1right)); :}
            ;

cond ::= expr:e1 LE expr:e2
         {: RESULT = new CompCond(e1, CompCond.LE, e2, loc(e1left, e1right)); :}
      | expr:e1 GE expr:e2
         {: RESULT = new CompCond(e1, CompCond.GE, e2, loc(e1left, e1right)); :}
      | expr:e1 EQ expr:e2
         {: RESULT = new CompCond(e1, CompCond.EQ, e2, loc(e1left, e1right)); :}
      | expr:e1 NE expr:e2
         {: RESULT = new CompCond(e1, CompCond.NE, e2, loc(e1left, e1right)); :}
      | expr:e1 LT expr:e2
         {: RESULT = new CompCond(e1, CompCond.LT, e2, loc(e1left, e1right)); :}
      | expr:e1 GT expr:e2
        {: RESULT = new CompCond(e1, CompCond.GT, e2, loc(e1left, e1right)); :}
      | cond:c1 AND cond:c2
         {: RESULT = new LogicalCond(c1, LogicalCond.AND, c2, loc(c1left, c1right)); :}
      | cond:c1 OR cond:c2
         {: RESULT = new LogicalCond(c1, LogicalCond.OR, c2, loc(c1left, c1right)); :}
      | NOT cond:c
         {: RESULT = new LogicalCond(c, LogicalCond.NOT, null, loc(cleft, cright)); :}
      | LPAREN cond:c RPAREN
         {: RESULT = c; :}
      ;